/*
 * SPRINT3 --version 0
 * author Caterina Leonelli, Matteo Longhi
 * project repository: https://github.com/RootLeo00/robot-coldstorage
 */

System /* -msglog */ coldstorageservice 

//--------Coldstorage e ServiceAccessGui Fase 1 -----------------------------------
Request storefood : storefood( KG ) 					//sender:serviceaccessgui receiver:coldstorageservice
Reply ticketaccepted : ticketaccepted( TICKETCODE, TICKETSECRET, TIMESTAMP ) 	//sender:coldstorageservice receiver:serviceaccessgui
Reply ticketdenied  : ticketdenied( ARG ) 		//sender:coldstorageservice receiver:serviceaccessgui
//-------------------------------------------------------------


//--------Coldstorage e ServiceAccessGui Fase 2 -----------------------------------
Request sendticket: sendticket(TICKETCODE, TICKETSECRET) //sender:serviceaccessgui receiver:coldstorageservice
Reply chargetaken : chargetaken(ARG)				//sender:coldstorageservice receiver:serviceaccessgui 
Reply ticketexpired: ticketexpired(ARG) 			//sender:coldstorageservice receiver:serviceaccessgui 
Reply ticketrejected: ticketrejected(ARG) 			//sender:coldstorageservice receiver:serviceaccessgui 
//-------------------------------------------------------------

 
//--------Transport trolley section-----------------------------------
Dispatch dodepositaction:dodepositaction(TICKETCODE) //sender:coldstorageservice receiver:transporttrolley
Event robotisinindoor:robotisinindoor(ARG) //emitted  by transporttrolley
Event robotisinhome:robotisinhome(ARG) //emitted  by transporttrolley
Event depositactionended:depositactionended(ARG) //emitted  by transporttrolley
Event robotismoving: robotismoving(ARG) //emitted  by transporttrolley
Event robotisstopped:robotisstopped(ARG) //emitted  by transporttrolley

//-------------------------------------------------------------

 
//--------From basicrobot-----------------------------------
Request moverobot    :  moverobot(TARGETX, TARGETY)
Reply moverobotdone  :  moverobotok(ARG)
Reply moverobotfailed:  moverobotfailed(PLANDONE, PLANTODO)
Event alarm           : alarm(X)
Event stopobstacle : stopobstacle(X)
Request engage : engage(ARG)
Reply   engagedone    : engagedone(ARG) 
Reply   engagerefused : engagerefused(ARG) 
//-------------------------------------------------------------


//--------Coldstorage e ColdRoom interaction -----------------------------------
Request howmanykgavailable : howmanykgavailable(ARG) //sender:coldstorageservice receiver:coldroom
Reply kgavailable : kgavailable(KG) //sender:coldroom receiver:coldstorageservice 
Dispatch updatekg : updatekg (KG) //sender:coldstorageservice receiver:coldroom
//-------------------------------------------------------------


 
//---------------Sonar e Led----------------------------------------------
Event sonardata : distance(D)   //emitted  by distancefilter
Event obstacle  : obstacle(D)   //emitted as stream by distancefilter
Dispatch ledCmd          : ledCmd(ONOFFBLINK) //sender:controllerled receiver:led
Event  endalarm          : endalarm(X) //emitted by sonar
//-------------------------------------------------------------------


//---------------ServiceAccessGui e Alieno interaction----------------------------------------------
Dispatch dostorefood:dostorefood(SESSIONNUMBER,KG)
Dispatch doinsertticket:doinsertticket(SESSIONNUMBER,KG)
//-------------------------------------------------------------------


Context ctxbasicrobot ip [host="127.0.0.1" port=8020 ]   
Context ctxcoldstorageservice ip [host="localhost" port=8038 ] 
ExternalQActor basicrobot context ctxbasicrobot  
 
 
 
 
/*  
 * ------------------------------------------------------------------
 *  coldstorageservice 
 * ------------------------------------------------------------------
*/      
QActor coldstorageservice context ctxcoldstorageservice{  
	 
		//*********** INTERAZIONE CON SERVICE ACCESS GUI
		[# 
			var Kgtoload : Int = 0; 
			var Expirationtime:Long = 100;
			var ticketList=it.unibo.ticket.TicketList(Expirationtime);
			#
		]
		State s0 initial {	  
			println("$name | wait for messages") color green
			//tutti i messaggi che arrivano quando sono in uno degli stati successivi
			//se non considerati, non vengono scartati, ma vengono memorizzati in coda e presi in carico successivamente
			//quando si andrà in uno stato che potrà processarli
			discardMsg Off  //default: discardMsg On
	    	  
	   	}  
	   	Transition t0 whenRequest storefood -> askhowmanykgoccupied
	    			whenRequest sendticket-> checkforticketexpired
	    			whenEvent robotisinindoor-> sendchargetaken
	    			whenEvent depositactionended -> updatecoldstorage
	  	
	  	///STOREFOOD REQUEST MANAGEMENT/////////////////
	  	State askhowmanykgoccupied{ 
	  		onMsg(storefood:storefood(KG)){
	  		[# Kgtoload = payloadArg(0).toInt(); #] 
	  		println("$name | chilograms to load $Kgtoload ") color green
	  		
	  		//chiedi a cold room quanto spazio è disponibile
	  		request coldroom -m howmanykgavailable : howmanykgavailable
	  		} 
		}
		Transition t0 whenReply kgavailable -> checkifthereisenoughspace
		
		State checkifthereisenoughspace {
			onMsg(kgavailable:kgavailable(KG)){
			[# var Kgavailable = payloadArg(0).toLong(); 
				var Totalkgtostore=ticketList.getTotalKgToStore();
			#]
			println("$name | total kg of current tickets: $Totalkgtostore") color green
			if [# Kgtoload <= Kgavailable-Totalkgtostore#] { 
				[# 
				var ticket=ticketList.createTicket(Kgtoload);
			   var TICKETCODE = ticket.getTicketNumber();
			   var TICKETSECRET= ticket.getTicketSecret();
			   var TIMESTAMP = ticket.getTimestamp(); 
			#]
				
				println("$name | ticket accepted with $TICKETCODE, $TICKETSECRET, $TIMESTAMP") color green
				replyTo storefood with ticketaccepted : ticketaccepted($TICKETCODE, $TICKETSECRET, $TIMESTAMP)
			}
			else{
				println("$name | ticket denied") color green
				replyTo storefood with ticketdenied : ticketdenied(ARG)
			}
				
			} 
					}   
	  	Goto s0
	  	///////////////////////////////////////////////////////////
	  	
	  	///////////////// SENDTICKET DISPATCH MANAGEMENT /////////////////
	  	State checkforticketexpired{
	  		//TODO
	  		onMsg(sendticket: sendticket(TICKETCODE, TICKETSECRET)){
	  			[#var TICKETCODE = payloadArg(0).toInt();
			   var TICKETSECRET= payloadArg(1);
			   var Ticket=ticketList.getTicket(TICKETCODE);
			   #]
			   if[#ticketList.isExpired(Ticket) #]{
			   	replyTo sendticket with ticketexpired: ticketexpired(ARG)
			   }else{
			   		if[#!Ticket.getTicketSecret().equals(TICKETSECRET) #]{
			   			replyTo sendticket with  ticketrejected: ticketrejected(ARG)
			   		}else{
			   			[#Ticket.setStatus(1);#]
						forward transporttrolley -m dodepositaction : dodepositaction($TICKETCODE)  	
			   		}	
			   }
			   
	  		}
	  	}
	  	Goto s0
	  	////////////////////////////////////////////////////
	  	
	  	//////////////// SEND CHARGETAKEN/////////////////
	  	State sendchargetaken{
	  		replyTo sendticket with chargetaken:chargetaken(ok)
	  	}
	  	Goto s0
	  	///////////////////////////////////////////////
	  	
	  	//////////////////////// COLDROOM STATUS UPDATE///////////////////////////////
	  	State updatecoldstorage {
	  		
	  		//add kg already arrived to cold storage
	  		onMsg(depositactionended:depositactionended(TICKETCODE)){
	  			[# var Ticketcode=payloadArg(0).toInt();
	  				#]
	  				println("$name | updating coldroom status ending operation number: $Ticketcode" ) color green
	  				[#
	  				var Ticket=ticketList.getTicket(Ticketcode);
	  				var Kg=Ticket.getKgToStore();
	  				var L=ticketList.toString();
	  				#
	  			]
	  			forward coldroom -m updatekg : updatekg($Kg)
	  			println("$L") color green
	  		[#
	  			ticketList.removeTicket(Ticketcode);
	  			L=ticketList.toString();#
	  		]
	  			println("$L") color green
	  		}
	  	}
	   	Goto s0
  		/////////////////////////////////////////////////////////////////
  	}
  

/*   
 * ------------------------------------------------------------------
 *  coldroom
 * ------------------------------------------------------------------
*/      
QActor coldroom context ctxcoldstorageservice{   
	[# var Kgstored : Long = 0 ;
		var MAXW : Long = 100;
		var Kgavailable=MAXW; #
	]   //TODO
	
	State s0 initial {	
		discardMsg Off 
		println("$name | active") color yellow
		
   	}  
	//wait request on info kg
	Transition t0 whenRequest howmanykgavailable -> replywithcoldroomspacestate
				  whenMsg updatekg -> handleupdateKg
	
	
	State replywithcoldroomspacestate{ 
		//send the actual state of the space
		println("$name | kg available $Kgavailable") color yellow
		replyTo howmanykgavailable with kgavailable : kgavailable($Kgavailable)
	}
	//wait request on info kg
	Goto s0
	
	//request to update kg
	State handleupdateKg{
		//update storage
		onMsg(updatekg:updatekg(KG)){
		[# 
			var insertkg = payloadArg(0).toLong();
			Kgstored += insertkg;
			Kgavailable-=insertkg;
		#]	
		println("$name | updated storage Kgavailable:$Kgavailable Kgstored:$Kgstored") color yellow
		}
		
	}
	Goto s0
}


/*  
 * ------------------------------------------------------------------
 *  transporttrolley
 * ------------------------------------------------------------------
*/      
QActor transporttrolley context ctxcoldstorageservice{     
	
	[#
		var COLDROOMX=4;
		var COLDROOMY=3;
		var INDOORX=0;
		var INDOORY=4;
		var HOMEX=0;
		var HOMEY=0;
		var TICKETCODE=-1;
		
		//var per gestire la alarm/endalarm
		var lastmove="";
		var startInstant:Long = 0;
		var endInstant : Long= 0;
		var deltatime: Long =0;
		val MINT: Long=40000;
		val DLIMIT = 70 ;
		var D = 0;
	#]
	State s0 initial{
		//set home,indoor,coldroom positions
		
	//usefull for keep tracking of coldstorageservice request operations
	discardMsg Off
	//try to engage basicrobot on startup
    println("$name |  request engage") color magenta
    request basicrobot -m engage : engage( transporttrolley , 330)
  	}
   	Transition t0 whenReply engagedone    -> waitforcommands
                whenReply engagerefused -> waitrobotfree
   
   //what to do if robot alredy engaged?
   	State waitrobotfree{ 
    println("$name | Sorry, the robot is already engaged.")  color magenta
  	}
  	State waitforcommands{
  	println("$name | waiting for commands.") color magenta
  	}
 	Transition t1 whenMsg dodepositaction -> moverobottoindoor
 				  
 	
 	
 	//move robot to indoor, then send chargetaken message and go to coldroom
 	State moverobottoindoor {
 		emit robotismoving : robotismoving //per gestire led!!!!!
 		[# lastmove = "moverobottoindoor" #]
 		//move robot to indoor
 		onMsg(dodepositaction :dodepositaction(TICKETCODE)){
 			[#
 				TICKETCODE=payloadArg(0).toInt();
 			#]
 		}
 		println("$name | moving robot to indoor.") color magenta
 		request basicrobot -m moverobot:moverobot($INDOORX,$INDOORY)
 	}
 	Transition t2 whenEvent obstacle  -> handleobstacle
 				  whenReply moverobotdone -> moverobottocoldroom
 				  whenReply moverobotfailed -> robotmovefailed 


	//when robot has taken kg from camion -> move robot to coldroom
	State moverobottocoldroom{
		emit robotismoving : robotismoving //per gestire led!!!!!
		[# lastmove = "moverobottocoldroom" #]
		//send event "robot is in indoor"
		println("$name | robot is in indoor") color magenta
		println("$name | moving robot to coldroom") color magenta
		emit robotisinindoor:robotisindoor(ok)
		request basicrobot -m moverobot:moverobot($COLDROOMX,$COLDROOMY)
	}
	Transition t3 whenEvent obstacle  -> handleobstacle
				  whenReply moverobotdone -> depositactionended
 				  whenReply moverobotfailed -> robotmovefailed
	
	State depositactionended{
		emit depositactionended:depositactionended($TICKETCODE)
		println("$name | robot is in coldroom") color magenta
		println("$name | depositaction ended") color magenta
		println("$name | waiting for next move" ) color magenta
		
	} 
	Transition t4 whenTime 100    -> moverobottohome
				  whenMsg dodepositaction -> moverobottoindoor
	 			  
	 
	State moverobottohome{
		emit robotismoving : robotismoving //per gestire led!!!!!
		[# lastmove = "moverobottohome" #]
		//send event "robot is in home"
		request basicrobot -m moverobot:moverobot($HOMEX,$HOMEY)
	}
	Transition t5 whenEvent obstacle  -> handleobstacle
				  whenReply moverobotdone -> emitrobotisinhome
 				  whenReply moverobotfailed -> robotmovefailed
 				  
 
 	State emitrobotisinhome{
 		[# lastmove = "" #]
 		emit robotisinhome: robotisinhome(ok) 
 		println("$name | robot is in home") color magenta
 	}Goto waitforcommands
 	
 	
 	/*
 	* end alarm or control failed robot moves
 	*/
 	State robotmovefailed{
 		println("$name | robot failed to move $lastmove") color red
 	}Goto restorelastmove //try again
 	
 	State handleobstacle{
 	 //attenzione se arriva allarme *dopo* resume e prima di MINT
 	 [# endInstant=  System.currentTimeMillis();   
 	 	deltatime= (endInstant - startInstant); //tempo trascorso dall'ultima resume
 	 #]
 	 println("$name | end-start= $deltatime >/< MINT=$MINT") color red
			println("$name handleobstacle ALARM") color red
			if [# startInstant==0L || deltatime >= MINT#] {emit alarm : alarm(obstacle)} //ricevuto da basicrobot
			else{println("$name alarm IGNORED") color magenta}
	}
	Transition t6 whenReply moverobotfailed -> alarmconsidered //allarme considerato e basicrobot ci dice che ha stoppato il robot dopi l'evento alarm
				  whenReply moverobotdone -> restorelastmove //allarme ignorato dato che il robot si è fermato perchè ha finito la mossa

//	State alarmconsidered{
//		println("$name reading sonardata") color red
//		onMsg( sonardata : distance(D)){
//			[# var D = payloadArg(0).toInt() #]
//			//Interact with the Led
//			if [# D >= DLIMIT #]{
//				println("$name | resume transport trolley") color red
//			}
//		}
//	}Transition t0 whenEvent sonardata and [# D >= DLIMIT #]-> restorelastmove
//	               whenEvent sonardata  -> alarmconsidered
//	
	
		 
	State alarmconsidered{
		//println("sonar | waits ... ") 		 
		updateResource [# "sonar waiting ..." #]
	}
	Transition t0 whenEvent sonardata -> handlesonardata
//	              whenEvent obstacle  -> handleobstacle
	
	State handlesonardata{
		printCurrentMessage
		updateResource [# "sonar handles $currentMsg" #]
		onMsg( sonardata : distance(D)){
			[# D = payloadArg(0).toInt() #]
			//Interact with the Led
			if [# D >= DLIMIT #]{
				println("$name | resume transport trolley") color red
//				emit endalarm : endalarm	
//				[# stopped = false #]
			}
		}
	}
	Goto restorelastmove if [# D >= DLIMIT #] else alarmconsidered
	
	
	State restorelastmove{
 		println("$name | restore to $lastmove") color red
 		[# startInstant=System.currentTimeMillis(); #]
 	}Goto moverobottoindoor if [# lastmove == "moverobottoindoor" #] else option2
	State option2{} Goto moverobottohome if [# lastmove == "moverobottohome" #] else option3
	State option3{} Goto moverobottocoldroom
}

 


 
/*  
 * ------------------------------------------------------------------
 *  sonar
 * ------------------------------------------------------------------
*/ 
CodedQActor sonarfisico  context ctxcoldstorageservice className "sonarSimulator"       //IN LOCALE
//CodedQActor sonar  context ctxsonarqak23 className "sonarHCSR04Support23"   //SU RASP
//CodedQActor datalogger     context ctxsonarqak23 className "rx.dataLogger"
CodedQActor datacleaner    context ctxcoldstorageservice className "rx.dataCleaner"
CodedQActor distancefilter context ctxcoldstorageservice className "rx.distanceFilter"

QActor sonar context ctxcoldstorageservice{ 
	[# val DLIMIT = 70 ; 
	   var stopped=false;
	#]
	State s0 initial{  
		println("sonar | start") 
		[# subscribeToLocalActor("distancefilter").subscribeToLocalActor("datacleaner").subscribeToLocalActor("sonarfisico") #]
	}
	Goto work
	 
	State work{
		//println("sonar | waits ... ") 		 
		updateResource [# "sonar waiting ..." #]
	}
	Transition t0 whenEvent sonardata -> handlesonardata
	              whenEvent obstacle  -> handleobstacle
	
	State handlesonardata{
		printCurrentMessage
		updateResource [# "sonar handles $currentMsg" #]
		onMsg( sonardata : distance(D)){
			[# var D = payloadArg(0).toInt() #]
			//Interact with the Led
			if [# D >= DLIMIT && stopped == true #]{
//				println("$name | resume transport trolley") color red
//				emit endalarm : endalarm	
				[# stopped = false #]
			}
		}
	}
	Goto work 
	
	State handleobstacle{
		onMsg( obstacle : obstacle(D)){			
//			println("$name handleobstacle ALARM ${payloadArg(0)}") color red
//			emit stopobstacle : stopobstacle
			[# stopped = true #]			
		}
		
	}
	Goto work  
	
}   



/*  
 * ------------------------------------------------------------------
 *  led
 * ------------------------------------------------------------------
*/ 
QActor led context ctxcoldstorageservice{ 
	State s0 initial{
		println("${name} | START") color magenta
	}  
	Transition t0 whenMsg ledCmd -> doCmd
	
	State doCmd{
		onMsg( ledCmd : ledCmd(V) ){
			[# var Cmd = payloadArg(0) #]
			if [# Cmd=="on" #]{
				println("${name} - on") color magenta	 		
			}
			if [# Cmd=="blink" #]{
				println("${name} - blink") color magenta
			}
			if [# Cmd=="off" #]{
				println("${name} - off") color magenta
			}
		}	 
	}
	Transition t0 whenMsg ledCmd -> doCmd
}


 /*   
 * ------------------------------------------------------------------
 *  controller sonar led
 * ------------------------------------------------------------------
*/ 
QActor controllerled context ctxcoldstorageservice{
	
	State s0 initial{
		println("${name} | START") color yellow
//		forward sonar -m sonaractivate : info(ok)
		forward led -m ledCmd : ledCmd(off) //robot nasce in home
	} 
	Transition t0 whenEvent robotismoving -> ledblink
				  whenEvent robotisinhome -> ledoff
				  whenEvent robotisstopped -> ledon
	
	State ledblink{
		 println("${name} - Turn the Led blink") color yellow
		 forward led -m ledCmd : ledCmd(blink)
	}
	Transition t0 whenEvent robotismoving -> ledblink
				  whenEvent robotisinhome -> ledoff 
				  whenEvent robotisstopped -> ledon
	
	State ledon{
		 println("${name} - Turn the Led on") color yellow
		 forward led -m ledCmd : ledCmd(on)
	}
	Transition t0 whenEvent robotismoving -> ledblink
				  whenEvent robotisstopped -> ledon
	
	
	State ledoff{
		 println("${name} - Turn the Led off") color yellow
		 forward led -m ledCmd : ledCmd(off)
	}
	Transition t0 whenEvent robotismoving -> ledblink
				  whenEvent robotisinhome -> ledoff
}


/*  
 * ------------------------------------------------------------------
 *  gui MOCK
 * ------------------------------------------------------------------
*/ 
 	QActor guimok context ctxcoldstorageservice{
	 	State s0 initial{
	 		discardMsg Off
	 		println("$name | start request") color magenta
	 		request coldstorageservice -m storefood:storefood(25)
	 	}
 		Transition t1 whenReply ticketaccepted -> sendticket
 		
 		State sendticket {
 		onMsg(ticketaccepted:ticketaccepted(TICKETCODE, TICKETSECRET,TIMESTAMP)){		
	 		[# 
				var Ticketcode= payloadArg(0);
				var Ticketsecret= payloadArg(1);
			#]
		println("$name | sending ticket to coldstorageservice") color magenta
		request coldstorageservice -m sendticket:sendticket($Ticketcode,$Ticketsecret)
//		delay 4000
//		emit stopobstacle: stopobstacle(10)
//		delay 4000
//		emit endalarm: endalarm
// 		
 		}
 		 
		
 		}
 		Transition t2 whenReply chargetaken-> end
 		State end{
 			println("$name | robot has taken the load") color magenta
 		}
 		Goto s0
 }